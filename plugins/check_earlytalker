#!perl -w
=head1 NAME

check_earlytalker - Check that the client doesn't talk before we send the SMTP banner

=head1 DESCRIPTION

Checks to see if the remote host starts talking before we've issued a 2xx
greeting.  If so, we're likely looking at a direct-to-MX spam agent which
pipelines its entire SMTP conversation, and will happily dump an entire spam
into our mail log even if later tests deny acceptance.

Depending on configuration, clients which behave in this way are either
immediately disconnected with a deny or denysoft code, or else are issued this
on all mail/rcpt commands in the transaction.

=head1 CONFIGURATION

=over 4

=item wait [integer]

The number of seconds to delay the initial greeting to see if the connecting
host speaks first.  The default is 1.  Do not select a value that is too high,
or you may be unable to receive mail from MTAs with short SMTP connect or
greeting timeouts -- these are known to range as low as 30 seconds, and may
in some cases be configured lower by mailserver admins.  Network transit time
must also be allowed for.

=item action [string: deny, denysoft, log]

What to do when matching an early-talker -- the options are I<deny>,
I<denysoft> or I<log>.

If I<log> is specified, the connection will be allowed to proceed as normal,
and only a warning will be logged.

The default is I<denysoft>.

=item defer-reject [boolean]

When an early-talker is detected, if this option is set to a true value, the
SMTP greeting will be issued as usual, but all RCPT/MAIL commands will be
issued a deny or denysoft (depending on the value of I<action>).  The default
is to react at the SMTP greeting stage by issuing the apropriate response code
and terminating the SMTP connection.

=item check-at [ CONNECT | DATA ]

Specifies when to check for early talkers. You can specify this option
multiple times to check more than once.

The default is I<check-at CONNECT> only.

=back

=cut

use strict;
use warnings;

use IO::Select;
use Qpsmtpd::Constants;

sub register {
  my ($self, $qp, @args) = @_;

  if (@args % 2) {
        $self->log(LOGERROR, "Unrecognized/mismatched arguments");
        return undef;
  }
  my %check_at;
  for (0..$#args) {
    next if $_ % 2;
    if (lc($args[$_]) eq 'check-at') {
      my $val = $args[$_ + 1];
      $check_at{uc($val)}++;
    }
  }
  if (!%check_at) {
    $check_at{CONNECT} = 1;
  }
  $self->{_args} = {
        'wait' => 1,
        'action' => 'denysoft',
        'defer-reject' => 0,
        @args,
        'check-at' => \%check_at,
  };
  if ( $qp->{conn} && $qp->{conn}->isa('Apache2::Connection')) {
      require APR::Const;
      APR::Const->import(qw(POLLIN SUCCESS));
      $self->register_hook('connect', 'apr_connect_handler');
      $self->register_hook('data', 'apr_data_handler');
  }
  else {
      $self->register_hook('connect', 'connect_handler');
      $self->register_hook('data', 'data_handler');
  }
  $self->register_hook('mail', 'mail_handler')
    if $self->{_args}->{'defer-reject'};
  1;
}

sub apr_connect_handler {
    my ($self, $transaction) = @_;

    return DECLINED unless $self->{_args}{'check-at'}{CONNECT};
    return DECLINED if ($self->connection->notes('whitelisthost'));
    my $ip = $self->qp->connection->remote_ip;

    my $c = $self->qp->{conn};
    my $socket = $c->client_socket;
    my $timeout = $self->{_args}->{'wait'} * 1_000_000;

    my $rc = $socket->poll($c->pool, $timeout, APR::Const::POLLIN());
    if ($rc == APR::Const::SUCCESS()) {
        $self->log(LOGNOTICE, "remote host started talking before we said hello [$ip]");
        if ($self->{_args}->{'defer-reject'}) {
            $self->connection->notes('earlytalker', 1);
        }
        else {
            my $msg = 'Connecting host started transmitting before SMTP greeting';
            return (DENY,$msg) if $self->{_args}->{'action'} eq 'deny';
            return (DENYSOFT,$msg) if $self->{_args}->{'action'} eq 'denysoft';
        }
    }
    else {
        $self->log(LOGINFO, "pass: remote host said nothing spontaneous");
    }
}

sub apr_data_handler {
    my ($self, $transaction) = @_;

    return DECLINED unless $self->{_args}{'check-at'}{DATA};
    return DECLINED if ($self->connection->notes('whitelisthost'));
    my $ip = $self->qp->connection->remote_ip;

    my $c = $self->qp->{conn};
    my $socket = $c->client_socket;
    my $timeout = $self->{_args}->{'wait'} * 1_000_000;

    my $rc = $socket->poll($c->pool, $timeout, APR::Const::POLLIN());
    if ($rc == APR::Const::SUCCESS()) {
        $self->log(LOGNOTICE, "remote host started talking before we said hello [$ip]");
        my $msg = 'Connecting host started transmitting before SMTP greeting';
        return (DENY,$msg) if $self->{_args}->{'action'} eq 'deny';
        return (DENYSOFT,$msg) if $self->{_args}->{'action'} eq 'denysoft';
    }
    else {
        $self->log(LOGINFO, "pass: remote host said nothing spontaneous");
    }
}

sub connect_handler {
  my ($self, $transaction) = @_;
  my $in = new IO::Select;
  my $ip = $self->qp->connection->remote_ip;

  return DECLINED unless $self->{_args}{'check-at'}{CONNECT};
  return DECLINED
      if ($self->connection->notes('whitelisthost'));

  $in->add(\*STDIN) || return DECLINED;
  if ($in->can_read($self->{_args}->{'wait'})) {
    $self->log(LOGNOTICE, "remote host started talking before we said hello [$ip]");
    if ($self->{_args}->{'defer-reject'}) {
        $self->connection->notes('earlytalker', 1);
    } else {
      my $msg = 'Connecting host started transmitting before SMTP greeting';
      return (DENY,$msg) if $self->{_args}->{'action'} eq 'deny';
      return (DENYSOFT,$msg) if $self->{_args}->{'action'} eq 'denysoft';
    }
  } else {
    $self->log(LOGINFO, 'pass: remote host said nothing spontaneous');
  }
  return DECLINED;
}

sub data_handler {
  my ($self, $transaction) = @_;
  my $in = new IO::Select;
  my $ip = $self->qp->connection->remote_ip;

  return DECLINED unless $self->{_args}{'check-at'}{DATA};
  return DECLINED
      if ($self->connection->notes('whitelisthost'));

  $in->add(\*STDIN) || return DECLINED;
  if ($in->can_read($self->{_args}->{'wait'})) {
    $self->log(LOGNOTICE, "remote host started talking before we said hello [$ip]");
    my $msg = 'Connecting host started transmitting before SMTP greeting';
    return (DENY,$msg) if $self->{_args}->{'action'} eq 'deny';
    return (DENYSOFT,$msg) if $self->{_args}->{'action'} eq 'denysoft';
  }
  else {
    $self->log(LOGINFO, 'pass: remote host said nothing spontaneous');
  }
  return DECLINED;
}

sub mail_handler {
  my ($self, $transaction) = @_;
  my $msg = 'Connecting host started transmitting before SMTP greeting';

  return DECLINED unless $self->connection->notes('earlytalker');
  return (DENY,$msg) if $self->{_args}->{'action'} eq 'deny';
  return (DENYSOFT,$msg) if $self->{_args}->{'action'} eq 'denysoft';
  return DECLINED;
}

1;

